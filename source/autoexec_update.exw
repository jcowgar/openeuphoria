		--------------------------------------
		--  Program to update AUTOEXEC.bat  --
		--  with EUDIR and PATH settings    --
		--  for Euphoria installation       --
		--  on old (pre-XP) systems         --
		--------------------------------------

without warning
include msgbox.e

constant KEYBOARD = 0
constant SUCCESS = 1, FAILURE = 0, NO_EDIT = 2
constant TRUE = 1, FALSE = 0
constant CLEAR = 0, NO_CLEAR = 2

-- from graphics.e:
constant M_GRAPHICS_MODE = 5,
	 M_SET_T_COLOR = 9,
	 M_VIDEO_CONFIG = 13,
	 M_GET_POSITION = 25

sequence cmd_line
cmd_line = command_line()

-- from wildcard.e:
constant TO_LOWER = 'a' - 'A'

function upper(object x)
    return x - (x >= 'a' and x <= 'z') * TO_LOWER
end function

procedure the_end(integer status)
-- exit install
    abort(status)
end procedure

-- Looks like the PATH can be longer than this now - just give a warning
constant MAX_PATH = 127

function edit_auto_exec(sequence eu_path)
-- edit the autoexec.bat file
-- add to the PATH, set EUDIR
    integer path_found, q, auto_exec_no, p, white, semi_pos, d, new_path_length
    sequence path, auto_exec, auto_name, base_name, answer, set_line, quote
    object line

    path = getenv("PATH")
    if sequence(path) then
	-- quick check for PATH already set correctly
	if match(upper(eu_path), upper(path)) then
	    return NO_EDIT
	end if
    end if

    set_line = "SET EUDIR=" & eu_path & "\n"
    eu_path = eu_path & "\\BIN"
    base_name = ":\\AUTOEXEC.BAT"
    auto_name = "C" & base_name
    auto_exec = {}
    path_found = 0

    auto_exec_no = open(auto_name, "r")
    if auto_exec_no = -1 then
	return FAILURE
    end if
    
    new_path_length = 0
    while auto_exec_no != -1 do
	-- read next line from autoexec.bat
	line = gets(auto_exec_no)
	if atom(line) then
	    exit
	end if
	
	p = match("EUDIR=", upper(line))
	q = match("EUDIR =", upper(line))
	if (p >= 1 and p <= 9) or (q >= 1 and q <= 9) then  
	    -- found an existing EUDIR= line
	    line = "\n"  -- delete it
	end if
	
	p = match("PATH", upper(line))
	if p then
	    -- line contains the word "PATH" in upper or lower case
	    -- at position p
	    white = TRUE
	    q = p - 1
	    while q >= 1 do
		if not find(line[q], " \t") then
		    -- non whitespace - only "SET" is allowed
		    if q >= 3 then
			if equal("SET", upper(line[q-2..q])) then
			    q -= 2
			else
			    white = FALSE
			    exit
			end if
		    else
			white = FALSE
			exit
		    end if
		end if
		q -= 1
	    end while

	    if white and find(line[p+4], " \t=") then
		-- this is a PATH line
		path_found += 1
		if path_found = 1 then
		    -- only change the first one encountered
		    if match(upper(eu_path), upper(line)) then
			-- its already there
			close(auto_exec_no)
			return NO_EDIT  -- without altering AUTOEXEC.BAT
		    end if
		    auto_exec = append(auto_exec, set_line)
		    semi_pos = find(';', line)
		    if semi_pos = 0 then
			semi_pos = length(line)
			line = line[1..length(line)-1] & ";\n"
		    end if
		    -- add eu_path to path
		    if find(' ', eu_path) then
			quote = "\""
		    else
			quote = ""
		    end if
		    line = line[1..semi_pos] & quote & eu_path & quote & ";" 
			   & line[semi_pos+1..length(line)]
		    new_path_length = length(line)
		end if
	    end if
	end if
	auto_exec = append(auto_exec, line)
    end while

    if auto_exec_no = -1 or not path_found then
	-- no autoexec.bat file, or no PATH command was found - make one:
	line = "SET PATH=%PATH%;" & eu_path & "\n"
	auto_exec &= {set_line, line}
	path_found = 1
    end if

    -- write out the new autoexec.bat
    if auto_exec_no != -1 then
	close(auto_exec_no)
    end if
    auto_exec_no = open(auto_name, "w")
    if auto_exec_no = -1 then
	return FAILURE
    end if
    for i = 1 to length(auto_exec) do
	puts(auto_exec_no, auto_exec[i])
    end for
    close(auto_exec_no)
    return SUCCESS
end function

procedure install()
-- main routine for Euphoria update of autoexec.bat
    sequence eu_path
    integer edit_status
    integer i, ok

    eu_path = ""
    if length(cmd_line) < 3 then      
	-- missing required path argument
    
    elsif length(cmd_line) >= 4 then   
	-- assume space in path and forgot quotes
	i = 3
	while i <= length(cmd_line) do
	    if length(eu_path) = 0 then
		eu_path = cmd_line[i]
	    else
		eu_path = eu_path & " " & cmd_line[i]
	    end if
	    i = i + 1
       end while
    else                         
	-- assume path was passed in as a parameter
	eu_path = cmd_line[3]
    end if

    if length(eu_path) = 0 then
	edit_status = FAILURE
    else
	edit_status = edit_auto_exec(eu_path)
    end if

    if edit_status = FAILURE then
	ok = message_box("To complete the installation you must edit AUTOEXEC.BAT manually.\n" &
			 "See the sections " &
			 "\'How to manually modify the environment in Windows (ME/98/95/3.1)\' " &
			 "and \'Modifying the Registry\' in HTML\\installing_euphoria.html.\n.",
			 "AUTOEXEC.BAT",
			 MB_OK
			 )
    
    elsif edit_status = NO_EDIT then
	ok = message_box("No changes were necessary in your AUTOEXEC.BAT file",
			 "AUTOEXEC.BAT",
			 MB_OK
			 )
    
    else
	ok = message_box("When the installation is completed you must\n" &
			 "shut down and restart your computer.\n" &
			 "This will set your PATH and EUDIR variables.",
			 "AUTOEXEC.BAT",
			 MB_OK
			 )
    end if
end procedure

install()

the_end(0)

