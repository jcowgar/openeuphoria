# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
pkgname=euphoria
pkgver=4.0.0
pkgrel=1
pkgdesc="Fast interpreted or compiled general purpose programming language"
url="http://openeuphoria.org"
license=('custom:"OpenEuphoria License"')
source=(http://downloads.sourceforge.net/rapideuphoria/Euphoria/$pkgver/Linux/$pkgname-$pkgver-src.tar.gz)
md5sums=('321d1e14a86e4ac4662c9d4f073fd9ea')
arch=('i686' 'x86_64')

if [ "$CARCH" = "x86_64" ]; then
  depends=('lib32-glibc')
fi

build() {
  cd "$srcdir/$pkgname-$pkgver-src/source"

  ./configure --prefix=/usr
  touch pcre/*
  make
  make tools
}

package() {
  cd "$srcdir/$pkgname-$pkgver-src/source"

  # Base and Tool install
  make DESTDIR="$pkgdir" install install-tools

  # Install our license file per ArchLinux spec
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 "$srcdir/$pkgname-$pkgver-src/License.txt" "$pkgdir/usr/share/licenses/$pkgname"

  # Create a default eu.cfg file
  mkdir -p "$pkgdir/etc/euphoria"
  echo [all] > "$pkgdir/etc/euphoria/eu.cfg"
  echo -i /usr/share/euphoria/include >> "$pkgdir/etc/euphoria/eu.cfg"
}

