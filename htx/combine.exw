-- combine a bunch of files into one
include misc.e
integer SLASH
if platform() = LINUX then
    SLASH = '/'
else
    SLASH = '\\'
end if

sequence ref_files, lib_files

ref_files = {
  "refman_0.doc",
  "refman_1.doc",
  "refman_2.doc",
  "refman_3.doc"
}

lib_files = {
  "lib_0.doc",
  "lib_type.htx",
  "lib_seq.htx",
  "lib_srch.htx",
  "lib_math.htx",
  "lib_bitw.htx",
  "lib_file.htx",
  "lib_mous.htx",
  "lib_os.htx",
  "lib_dbg.htx",
  "lib_grap.htx",
  "lib_mach.htx",
  "lib_dyn.htx",
  "lib_c.htx",
  "lib_map.htx",
  "lib_dtm.htx",
  "lib_task.htx",
  "lib_eds.htx"
}

procedure quit(sequence msg)
    puts(2, msg & '\n')
    puts(2, "Contact Rapid Deployment Software, rds@RapidEuphoria.com\n")
    if getc(0) then
    end if
    abort(1)
end procedure

sequence cmd_line, app_dir
integer param

cmd_line = command_line()

if length(cmd_line) < 3 then
    quit("no dir was passed to combine")

elsif length(cmd_line) >= 4 then
    -- space in path and forgot quotes
    app_dir = cmd_line[3]
    param = 4
    while param <= length(cmd_line) do
	app_dir = app_dir & " " & cmd_line[param]
	param += 1
    end while

else  -- 3
    app_dir = cmd_line[3] 
end if

function try_open(sequence filename, sequence mode)
    integer fn
    
    fn = open(filename, mode)
    if fn != -1 then
	return fn
    end if
    quit("\nCouldn't open " & filename)
end function

integer fn, ref_out, lib_out
object line

ref_out = try_open(app_dir & SLASH & "doc" & SLASH &"REFMAN.DOC", "w")
puts(ref_out, 
"[To view this manual with your Web browser, double-click on html" & SLASH & "refman.htm]\n\n")
for i = 1 to length(ref_files) do
    fn = try_open(ref_files[i], "r")
    while 1 do
	line = gets(fn)
	if atom(line) then
	    exit
	end if
	puts(ref_out, line)
    end while
    close(fn)
end for
close(ref_out)

lib_out = try_open(app_dir & SLASH & "doc" & SLASH & "LIBRARY.DOC", "w")
for i = 1 to length(lib_files) do
    fn = try_open(lib_files[i], "r")
    while 1 do
	line = gets(fn)
	if atom(line) then
	    exit
	end if
	puts(lib_out, line)
    end while
    close(fn)
end for
close(lib_out)


