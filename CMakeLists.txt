#
# CMake build file for Euphoria (All Platforms, All Compilers)
#

#
# CMake settings
#

CMAKE_MINIMUM_REQUIRED( VERSION 2.4 )

SET( CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS true )
SET( CMAKE_BUILD_TYPE "Release" )

INCLUDE( "${CMAKE_SOURCE_DIR}/source/CMakeMacros.cmake" )

#
# Project Setup
#

PROJECT( Euphoria )
SET( VER_MAJ 4 )
SET( VER_MIN 0 )
SET( VER_PAT 0 )
SET( VER_TYPE "development" )


INCLUDE(InstallRequiredSystemLibraries)

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Euphoria Programming Language")
SET(CPACK_PACKAGE_VENDOR "The OpenEuphoria Group")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/file_id.diz")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/License.txt")
SET(CPACK_PACKAGE_VERSION_MAJOR "${VER_MAJ}")
SET(CPACK_PACKAGE_VERSION_MINOR "${VER_MIN}")
SET(CPACK_PACKAGE_VERSION_PATCH "${VER_PAT}")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "Euphoria${VER_MAJ}.${VER_MIN}")
IF(WIN32 AND NOT UNIX)
  # There is a bug in NSI that does not handle full unix paths properly. Make
  # sure there is at least one set of four (4) backlasshes.
  #SET(CPACK_PACKAGE_ICON "${CMake_SOURCE_DIR}/Utilities/Release\\\\InstallIcon.bmp")
  SET(CPACK_NSIS_INSTALLED_ICON_NAME "${CMAKE_CURRENT_SOURCE_DIR}\\\\source\\\\euphoria.ico")
  SET(CPACK_NSIS_DISPLAY_NAME "${CPACK_PACKAGE_INSTALL_DIRECTORY} RapidEuphoria")
  SET(CPACK_NSIS_HELP_LINK "http:\\\\\\\\www.openeuphoria.org\\\\EUforum\\\\")
  SET(CPACK_NSIS_URL_INFO_ABOUT "http:\\\\\\\\www.rapideuphoria.com")
  SET(CPACK_NSIS_CONTACT "jeremy@cowgar.com")
  SET(CPACK_NSIS_MODIFY_PATH ON)
ELSE()
  SET(CPACK_STRIP_FILES "bin/eui.exe" "bin/euc.exe" "bin/eub.exe" )
  SET(CPACK_SOURCE_STRIP_FILES "")
ENDIF()
SET(CPACK_PACKAGE_EXECUTABLES "eui" "Euphoria Interpreter")
INCLUDE(CPack)

#
# Options
#

OPTION( USE_INT_CODES   "Use INT_CODES"                  OFF )
IF( NOT UNIX AND NOT MINGW )
  OPTION( USE_MANAGED_MEM "Use Managed Memory"           ON  )
ENDIF()

OPTION( BUILD_RELEASE   "Build a release version"        OFF )
OPTION( BUILD_TOOLS     "Build the tools (eutest/eudoc)" OFF )
OPTION( BUILD_DOCS      "Build the documentation"        OFF )

#
# General Installation
#

INSTALL( DIRECTORY include DESTINATION "./"
  FILE_PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
  DIRECTORY_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE
      WORLD_READ WORLD_EXECUTE
  COMPONENT core
  PATTERN ".svn" EXCLUDE
  PATTERN "creole" EXCLUDE )

INSTALL( DIRECTORY demo DESTINATION "./"
  FILE_PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
  DIRECTORY_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE
      WORLD_READ WORLD_EXECUTE
  COMPONENT demo
  PATTERN ".svn" EXCLUDE
  PATTERN "*.exe" EXCLUDE
  PATTERN "*.err" EXCLUDE
  PATTERN "*.edb" EXCLUDE )

INSTALL( DIRECTORY tutorial DESTINATION "./"
  FILE_PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
  DIRECTORY_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE
      WORLD_READ WORLD_EXECUTE
  COMPONENT demo
  PATTERN ".svn" EXCLUDE
  PATTERN "*.exe" EXCLUDE
  PATTERN "*.err" EXCLUDE
  PATTERN "tutorial/*" )

INSTALL( DIRECTORY source DESTINATION "./"
  FILE_PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
  DIRECTORY_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE
      WORLD_READ WORLD_EXECUTE
  COMPONENT source
  PATTERN ".svn" EXCLUDE
  PATTERN "*.o" EXCLUDE
  PATTERN "*.obj" EXCLUDE
  PATTERN "build*" EXCLUDE
  PATTERN "source/*" )

SET( CPACK_COMPONENTS_ALL core tool demo source doc )

CPACK_ADD_COMPONENT( core
  DISPLAY_NAME  "Core"
  DESCRIPTION   "Core Euphoria Files (binaries and includes)"
  REQUIRED )
CPACK_ADD_COMPONENT( tool
  DISPLAY_NAME  "Tools"
  DESCRIPTION   "Optional tools such as EuTest, EuDOC and others"
  INSTALL_TYPES Full Common )
CPACK_ADD_COMPONENT( demo
  DISPLAY_NAME  "Demos/Tutorials"
  DESCRIPTION   "Demo and Tutorial Euphoria files"
  INSTALL_TYPES Full Common )
CPACK_ADD_COMPONENT( source
  DISPLAY_NAME  "Source Code"
  DESCRIPTION   "Source code for the interpreter, translator and backend"
  INSTALL_TYPES  Full )
CPACK_ADD_COMPONENT( doc
  DISPLAY_NAME  "Documentation"
  DESCRIPTION   "Text, HTML and PDF documentation"
  INSTALL_TYPES Full Common )

#
# Sub Directories
#

ADD_SUBDIRECTORY( source )
